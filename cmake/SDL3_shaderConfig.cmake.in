# sdl3_shader cmake project-config input for CMakeLists.txt script

include(FeatureSummary)
set_package_properties(SDL3_shader PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_shader/"
    DESCRIPTION "Support for shader translation with Simple Directmedia Layer"
)

set(SDL3_shader_FOUND ON)

if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/SDL3_shader-shared-targets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/SDL3_shader-shared-targets.cmake")
endif()

if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/SDL3_shader-static-targets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/SDL3_shader-static-targets.cmake")
endif()

function(_sdl_create_target_alias_compat NEW_TARGET TARGET)
    if(CMAKE_VERSION VERSION_LESS "3.18")
        # Aliasing local targets is not supported on CMake < 3.18, so make it global.
        add_library(${NEW_TARGET} INTERFACE IMPORTED)
        set_target_properties(${NEW_TARGET} PROPERTIES INTERFACE_LINK_LIBRARIES "${TARGET}")
    else()
        add_library(${NEW_TARGET} ALIAS ${TARGET})
    endif()
endfunction()

# Make sure SDL3_shader::SDL3_shader always exists
if(NOT TARGET SDL3_shader::SDL3_shader)
    if(TARGET SDL3_shader::SDL3_shader-shared)
        _sdl_create_target_alias_compat(SDL3_shader::SDL3_shader SDL3_shader::SDL3_shader-shared)
    else()
        _sdl_create_target_alias_compat(SDL3_shader::SDL3_shader SDL3_shader::SDL3_shader-static)
    endif()
endif()
